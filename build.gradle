import org.gradle.internal.os.OperatingSystem

project.ext.lwjglVersion = "3.2.1"

switch (OperatingSystem.current()) {
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
    }
}

apply plugin: "org.sonarqube"

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven'

    group 'com.github.unknowndomaingame'
    version '0.1.0-alpha'

    sourceCompatibility = targetCompatibility = 11

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven {
            url 'https://jitpack.io'
        }
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url "https://oss.sonatype.org/content/repositories/releases/"
        }
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.1'
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
}
